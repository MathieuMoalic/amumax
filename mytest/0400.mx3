lattice_param := 500e-9
ring_size := 50e-9
ad_size := 200e-9
PBC := 32
Tx := 500.00000e-9
Ty := 500.00000e-9
Tz := 13.20000e-9
Nx := 512
Ny := 512
Nz := 1
setgridsize(Nx,Ny,Nz)
setcellsize(Tx/Nx,Ty/Ny,Tz/Nz)
setpbc(PBC,PBC,0)
edgesmooth=3
    
// CoPd film
msat = 810e3
aex = 13e-12
ku1 = 453195
anisu = vector(0,0,1)
alpha = 1e-7
gammall = 187e9

// Geom
ad := rect(ad_size,ad_size).RotZ(pi/4)
ring := rect(ad_size + ring_size * 2,ad_size + ring_size * 2).RotZ(pi/4)

rings := ring
ads := ad
m.setinshape(ring,vortex(1,1))

bulk := universe().sub(rings).sub(ads)
m.setInShape(bulk,uniform(0,0,1))
defregion(201,rings)
defregion(202,ads)
defregion(203,bulk)
ku1.setregion(201,0)
setgeom(bulk.add(rings).sub(ads))

// Static
phi := 0.0001 * pi / 180
theta := 0.0001 * pi / 180
B0 := 0.400
B_ext = vector(B0*sin(theta)*cos(phi), B0*sin(theta)*sin(phi), B0*cos(theta))

// Relaxation
maxerr = 4e-5
minimizerstop = 1e-8
relax()
minimize()

// Dynamics
setsolver(5)
maxdt = 1.42e-12
mindt = 1e-14
maxerr = 0.5e-7
amps:= 5e-4
f_cut := 20e9
t_sampl := 0.5 / (f_cut * 1.5)
t0 := t + 10/f_cut

// Bmask
grainSize  := 20e-9 
randomSeed := 1234567
maxRegion  := 60
ext_makegrains(grainSize, maxRegion, randomSeed)
for i:=0; i<maxRegion; i++{
    b:=(rand()-0.5)*0.2/f_cut
    B_ext.setregion(i, vector(
        B0*sin(theta)*cos(phi)+amps*sinc(2*pi*f_cut*(t-t0+b)),
        B0*sin(theta)*sin(phi)+amps*sinc(2*pi*f_cut*(t-t0+b)),
        B0*cos(theta),
    ))
    defregion(i+maxRegion, ShapeFromRegion(i).intersect(ring))
    Ku1.SetRegion(i+maxRegion, 0)
    B_ext.setregion(i+maxRegion, vector(
        B0*sin(theta)*cos(phi)+amps*sinc(2*pi*f_cut*(t-t0+b)),
        B0*sin(theta)*sin(phi)+amps*sinc(2*pi*f_cut*(t-t0+b)),
        B0*cos(theta),
    ))
}
minimize()
relax()
saveas(m,"stable")
saveas(regions,"regions")
saveas(ku1,"ku1")
saveas(torque,"torque")
saveas(b_ext,"b_ext")
saveas(b_demag,"b_demag")
saveas(b_eff,"b_eff")
saveas(geom, "geom")

// Saving
run(20/f_cut)
B_ext.RemoveExtraTerms( )
B_ext = vector(B0*sin(theta)*cos(phi), B0*sin(theta)*sin(phi), B0*cos(theta))
run(5/f_cut)
t = 0
tableadd(m)
tableadd(B_ext)
tableautosave(t_sampl)
autosave(m,t_sampl)
run(1000 * t_sampl)